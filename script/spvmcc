#!perl

use strict;
use warnings;

use SPVM();
use SPVM::Builder::Exe;
use SPVM::Builder::Util;

SPVM::Builder::Util::getopt
  'h|help'          => \my $help,
  'v|version'       => \my $show_version,
  'o|output=s'        => \my $output_file,
  'B|build-dir=s'   => \my $build_dir,
  'I|class-path=s' => \my @class_paths,
  'q|quiet'   => \my $quiet,
  'f|force' => \my $force,
  'c|config=s' => \my $config_file,
  'p|print-dependent-resources' => \my $print_dependent_resources,
;

if ($help) {
  print SPVM::Builder::Util::extract_usage;
}
elsif ($show_version) {
  my $version_string = "spvmcc v$SPVM::VERSION";
  print "$version_string\n";
}
else {
  my $class_name = shift;
  unless (defined $class_name) {
    die SPVM::Builder::Util::extract_usage;
  }
  
  my @original_class_paths = @class_paths;
  push @class_paths, map { "$_/SPVM" } @INC;
  unshift @INC, map { $_ =~ s/[\\\/]SPVM$//; $_; } @original_class_paths;

  my $build_exe = SPVM::Builder::Exe->new(
    build_dir => $build_dir,
    class_paths => \@class_paths,
    output_file => $output_file,
    class_name => $class_name,
    quiet => $quiet,
    force => $force,
    config_file => $config_file,
  );
  if ($print_dependent_resources) {
    my $lines = $build_exe->get_dependent_resource_lines;
    print "$_\n" for @$lines;
  }
  else {
    $build_exe->build_exe_file;
  }
}


=encoding utf8

=head1 Name

spvmcc - SPVM compiler to create exe file

=head1 Usage

  usage: spvmcc [<options>] <class_name>
    
    spvmcc -I lib/SPVM -o myapp Myapp

  options:
    -h, --help                     Show this message
    -v, --version                  Show the version
    -o, --output                   Output file name
    -I, --class-path <directory>   Add SPVM class path
    -B, --build-dir <directory>    Building diretory
    -q, --quiet                    Quiet output
    -f, --force                    Force compile and link
    -c, --config                   Config file
    
=head1 Description

spvmcc is SPVM compiler to create executable file.

=head1 Copyright & License

Copyright 2023 Yuki Kimoto. All Rights Reserved.

MIT License.
