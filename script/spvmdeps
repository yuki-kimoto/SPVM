#!perl

use strict;
use warnings;

use SPVM();
use SPVM::Builder::DependencyAnalyzer;
use SPVM::Builder::Util;
use FindBin;

SPVM::Builder::Util::getopt
  # spvmdeps and spvm shared
  'h|help'          => \my $help,
  'v|version'       => \my $show_version,
  'I|include-dir=s' => \my @include_dirs,
  
  # spvmdeps only
  'o|output=s'        => \my $output_file,
  'resource-info' => \my $show_resource_info,
  'cpanm' => \my $cpanm_option_on,
  'with-version' => \my $with_version_option_on,
;

if ($help) {
  print SPVM::Builder::Util::extract_usage;
  exit 0;
}
elsif ($show_version) {
  my $version_string = "spvmdeps v$SPVM::VERSION";
  print "$version_string\n";
  exit 0;
}

my $script_name = shift;

my $source = "";

$0 = $script_name;

FindBin::again();

eval { $source = SPVM::Builder::Util::slurp_binary($script_name); };

my $include_dirs_by_lib_directive = SPVM::Builder::Util::parse_lib_directive($source, $FindBin::Bin);

unshift @INC, map { $_ =~ s/[\\\/]SPVM$//; $_; } @include_dirs, @$include_dirs_by_lib_directive;

my $dependency_analyzer = SPVM::Builder::DependencyAnalyzer->new(script_name => $script_name, with_version => $with_version_option_on);

unless (defined $script_name) {
  die "[spvmdeps command]spvmdeps command needs <script_name>."
}

if ($show_resource_info) {
  
  my $resource_info = $dependency_analyzer->dump_resource_info;
  
  print $resource_info;
}
elsif ($cpanm_option_on) {
  
  my $cpanm_commands = $dependency_analyzer->to_cpanm_commands;
  
  my $cpanm_commands_lines = SPVM::Builder::DependencyAnalyzer->to_lines($cpanm_commands);
  
  print $cpanm_commands_lines;
}
else {
  
  my $classes = $dependency_analyzer->to_classes;
  
  my $classes_lines = SPVM::Builder::DependencyAnalyzer->to_lines($classes);
  
  print $classes_lines;
}

=encoding utf8

=head1 Name

spvmdeps - Generating Excutable File

=head1 Description

The spvmdeps command generates an executable file from SPVM classes.

=head1 Usage

  usage: spvmdeps [<options>] <script_name>
    
    spvmdeps -o myapp myapp.spvm
    
    spvmdeps -I lib/SPVM -o myapp myapp.spvm
  
  options:
    -h, --help                      Shows this message
    -v, --version                   Shows the version
    -I, --include-dir <directory>   Adds a include directory
    --resource-info                 Show config files of dependent resources
    --cpanm                         Prints cpanm commands with dependent cpan modules and their versions

=head1 Details

  spvmdeps [<options>] <script_name>

The C<spvmdeps> command prints class dependency information.

Output Example:

  Sys 1.201
  Regex 0.980
  Foo (version_from Sys)
  Bar

C<E<lt>optionsE<gt>> are L<options|/"Options">.

C<E<lt>script_nameE<gt>> is a script name that contains a L<bootstrap method|SPVM::Document::Language::Class/"Bootstrap Method"> in an L<anon class|SPVM::Document::Language::Class/"Anon Class">.

  class {
    static method main : void () {
      
    }
  }

See L<Class Search Directories|SPVM::Document::Language::Class/"Class Search Directories"> about default class search directories.

See L<SPVM::Document::EnvironmentVariables> about available environment variables.

=head2 Resources

There are important points to be aware of when generating executable files. That is, L<resources|SPVM::Document::Resource> are not automatically compiled.

When you run an SPVM program with the L<spvm> command, the resources are contained within the shared library of each class. Therefore, there are no conflicts between resources.

However, in the case of executable files, there are resource conflicts. For this, the resources must be resolved manually in the configuration file.

  $config->use_resource('Resource::Zlib');

This is hard work, but given that the executable file must be compiled from source files and run on a variety of platforms, I think that solving it manually is a better way.

I have published a command that allows you to view the list of classes using resources and the resource settings.

L<How to dump resource information|https://github.com/yuki-kimoto/SPVM/wiki/Config#how-to-dump-resource-information>

=head1 Options

=head2 --help

Outputs how to use the C<spvmdeps> command to standard output.

=head2 -h

  -h

Same as L</"--help">.

=head2 --version

Outputs the version of the C<spvmdeps> command to standard output. This version is the same as the version of L<SPVM>.

=head2 -v

  -v

Same as L</"--version">.

=head2 --include-dir

  --include-dir <directory>

Prepends C<E<lt>directoryE<gt>> to L<class search directories|SPVM::Document::Language::Class/"Class Search Directories">

This option can be specified multiple times.

  --include-dir dir1 --include-dir dir2

In this case, class search directories becomes the following.

  [dir1, dir2, default_dirs]

=head2 -I

  -I <directory>

Same as L</"--include-dir">.

=head2 --resource-info

  --resource-info

Shows all config files loading resources.

=head2 --cpanm

  --cpanm

Prints cpanm commands with all dependent cpan modules and their versions.

Output Example:

  cpanm SPVM@1.001
  cpanm SPVM::Sys@1.201
  cpanm SPVM::Regex@0.980
  cpanm SPVM::Foo

=head1 lib Directive

If the source code specified by C<E<lt>script_nameE<gt>> contains lib directives, The directories specified by lib directive is prepeneded to L<class search directories|SPVM::Document::Language::Class/"Class Search Directories">.
  
  #lib "$FindBin::Bin/lib"
  
  class {
  
  }

This directories specified by lib directive is placed after the directories specified by L</"--include-dir"> option.

=head1 Copyright & License

Copyright 2023 Yuki Kimoto. All Rights Reserved.

MIT License.
