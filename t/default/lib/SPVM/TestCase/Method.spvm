class TestCase::Method {
  use TestCase::Minimal;
  use TestCase::Point_3b;
  use TestCase::Point_3s;
  use TestCase::Point_3i;
  use TestCase::Point_3l;
  use TestCase::Point_3f;
  use TestCase::Point_3d;
  use Comparator::Object;
  use TestCase::Simple as TS;
  use Fn;

  # Use keyword name as method name
  static method int : int () {
    
    return 5;
  }
  method if : int () {
    
    return 3;
  }
  static method _under_score : int () {
    return 4;
  }

  static method call_keyword_name_method : int () {
    
    # Call a static method with parenthes
    {
      my $num = TestCase::Method->int();
      unless ($num == 5) {
        return 0;
      }
    }
    
    # Call a static method without parenthes
    {
      my $num = TestCase::Method->int;
      unless ($num == 5) {
        return 0;
      }
    }
    
    # Call a static method with parenthes and current class
    {
      my $num = &int();
      unless ($num == 5) {
        return 0;
      }
    }
    
    # Call a static method without parenthes and current class
    {
      my $num = &int;
      unless ($num == 5) {
        return 0;
      }
    }
    
    # Call a instance method with parenthes
    {
      my $method = new TestCase::Method;
      my $num = $method->if();
      unless ($num == 3) {
        return 0;
      }
    }

    # Call a instance method without parenthes
    {
      my $method = new TestCase::Method;
      my $num = $method->if;
      unless ($num == 3) {
        return 0;
      }
    }

    # Call a static method with current class that starts under score.
    {
      my $num = &_under_score();
      unless ($num == 4) {
        return 0;
      }
    }
    
    return 1;
  }

  static method test_import_method : int () {
    unless (TS->import_method1() == 1) {
      return 0;
    }
    
    unless (TS->import_method2() == 2) {
      return 0;
    }
    
    return 1;
  }
  
  static method already_exists_method_no_arg : int () {
    return 3;
  }

  static method already_exists_method : int ($num1 : int, $num2 : int) {
    return $num1 + $num2;
  }

  static method test_return_value_automatical_numeric_convertion_widening : double () {
    
    return 2;
  }

  static method test_return_value_automatical_numeric_convertion_narrowing_byte : byte () {
    
    return 127;
  }

  static method test_return_value_automatical_numeric_convertion_narrowing_short : short () {
    
    return 32767;
  }
  
  static method return_value_automatical_numeric_convertion : int () {
    {
      my $num : double = &test_return_value_automatical_numeric_convertion_widening();
      unless ($num == 2.0) {
        return 0;
      }
    }
    
    {
      my $num : byte = &test_return_value_automatical_numeric_convertion_narrowing_byte();
      unless ($num == 127) {
        return 0;
      }
    }

    {
      my $num : short = &test_return_value_automatical_numeric_convertion_narrowing_short();
      unless ($num == 32767) {
        return 0;
      }
    }
    
    return 1;
  }
  
  static method test_vaarg_objects_pass_array : int ($string : string, $nums : object[]...) {
    
    unless ($string eq "a") {
      return 0;
    }
    
    unless (@$nums == 3) {
      return 0;
    }
    
    unless ((int)$nums->[0] == 1 && (int)$nums->[1] == 2 && (int)$nums->[2] == 3) {
      return 0;
    }
    
    return 1;
  }
  
  static method vaarg_objects_pass_array : int () {
    return TestCase::Method->test_vaarg_objects_pass_array("a", [(object)1, 2, 3]);
  }

  static method test_vaarg_objects_pass_each_values : int ($string : string, $nums : object[]...) {
    
    unless ($string eq "a") {
      return 0;
    }
    
    unless (@$nums == 3) {
      return 0;
    }
    
    unless ((int)$nums->[0] == 1 && (int)$nums->[1] == 2 && (int)$nums->[2] == 3) {
      return 0;
    }
    
    return 1;
  }

  static method test_vaarg_objects_pass_empty : int ($string : string, $nums : object[]...) {
    
    unless (@$nums == 0) {
      return 0;
    }
    
    return 1;
  }

  static method vaarg_objects_pass_each_values : int () {
    return TestCase::Method->test_vaarg_objects_pass_each_values("a", 1, 2, 3);
  }

  static method vaarg_objects_pass_empty : int () {
    return TestCase::Method->test_vaarg_objects_pass_empty("a");
  }

  static method test_vaarg_pass_array : int ($string : string, $nums : int[]...) {
    
    unless ($string eq "a") {
      return 0;
    }
    
    unless (@$nums == 3) {
      return 0;
    }
    
    unless ($nums->[0] == 1 && $nums->[1] == 2 && $nums->[2] == 3) {
      return 0;
    }
    
    return 1;
  }
  
  static method vaarg_pass_array : int () {
    return TestCase::Method->test_vaarg_pass_array("a", [1, 2, 3]);
  }

  static method test_vaarg_pass_each_values : int ($string : string, $nums : int[]...) {
    
    unless ($string eq "a") {
      return 0;
    }
    
    unless (@$nums == 3) {
      return 0;
    }
    
    unless ($nums->[0] == 1 && $nums->[1] == 2 && $nums->[2] == 3) {
      return 0;
    }
    
    return 1;
  }
  
  static method vaarg_pass_each_values : int () {
    return TestCase::Method->test_vaarg_pass_each_values("a", 1, 2, 3);
  }
  
  static method cb_obj_capture : int () {
    my $capture1 = 7;
    my $capture2 = 10;
    my $cb_obj = [$capture1 : int, $capture2 : int] method : int ($x1 : object, $x2 : object) {
      
      unless ($capture1 == 7) {
        return 0;
      }
      
      unless ($capture2 == 10) {
        return 0;
      }
      
      unless ($self->{capture1} == 7) {
        return 0;
      }

      $self->{capture1} = 5;
      
      unless ($capture1 == 5) {
        return 0;
      }
      
      return 1;
    };
    
    return $cb_obj->(undef, undef);
  }
  
  static method cb_obj_call_cb_obj : int () {
    my $cb_obj = method : int ($x1 : int, $x2 : int) {
      return $x1 + $x2;
    };
    
    my $total = $cb_obj->(1, 3);
    
    unless ($total == 4) {
      return 0;
    }
    
    return 1;
  }

  static method cb_obj_call_cb_obj_from_callback : int () {
    my $cb_obj = method : int ($x1 : object, $x2 : object) {
      my $x1_num = (int)$x1;
      my $x2_num = (int)$x2;
      return $x1_num + $x2_num;
    };
    
    my $comparator : Comparator::Object = $cb_obj;
    
    my $total = $comparator->(2, 3);
    
    unless ($total == 5) {
      return 0;
    }
    
    return 1;
  }
  
  static method return_value_byte_method : TestCase::Point_3b () {
    my $value : TestCase::Point_3b;
    $value->{x} = Fn->INT8_MIN();
    $value->{y} = 1;
    $value->{z} = 2;
    
    return $value;
  }
  
  static method return_value_byte : int () {
    my $value : TestCase::Point_3b = TestCase::Method->return_value_byte_method();
    
    if ($value->{x} == Fn->INT8_MIN()) {
      if ($value->{y} == 1) {
        if ($value->{z} == 2) {
          return 1;
        }
      }
    }
    
    return 0;
  }
  
  static method return_value_short_method : TestCase::Point_3s () {
    my $value : TestCase::Point_3s;
    $value->{x} = Fn->INT16_MIN();
    $value->{y} = 1;
    $value->{z} = 2;
    
    return $value;
  }
  
  static method return_value_short : int () {
    my $value : TestCase::Point_3s = TestCase::Method->return_value_short_method();
    
    if ($value->{x} == Fn->INT16_MIN()) {
      if ($value->{y} == 1) {
        if ($value->{z} == 2) {
          return 1;
        }
      }
    }
    
    return 0;
  }
  
  static method return_value_int_method : TestCase::Point_3i () {
    my $value : TestCase::Point_3i;
    $value->{x} = Fn->INT32_MIN();
    $value->{y} = 1;
    $value->{z} = 2;
    
    return $value;
  }
  
  static method return_value_int : int () {
    my $value : TestCase::Point_3i = TestCase::Method->return_value_int_method();
    
    if ($value->{x} == Fn->INT32_MIN()) {
      if ($value->{y} == 1) {
        if ($value->{z} == 2) {
          return 1;
        }
      }
    }
    
    return 0;
  }
  
  static method return_value_long_method : TestCase::Point_3l () {
    my $value : TestCase::Point_3l;
    $value->{x} = Fn->INT64_MIN();
    $value->{y} = 1;
    $value->{z} = 2;
    
    return $value;
  }
  
  static method return_value_long : int () {
    my $value : TestCase::Point_3l = TestCase::Method->return_value_long_method();
    
    if ($value->{x} == Fn->INT64_MIN()) {
      if ($value->{y} == 1) {
        if ($value->{z} == 2) {
          return 1;
        }
      }
    }
    
    return 0;
  }
  
  static method return_value_float_method : TestCase::Point_3f () {
    my $value : TestCase::Point_3f;
    $value->{x} = Fn->FLT_MIN();
    $value->{y} = 0.25f;
    $value->{z} = 0.5f;
    
    return $value;
  }
  
  static method return_value_float : int () {
    my $value : TestCase::Point_3f = TestCase::Method->return_value_float_method();
    
    if ($value->{x} == Fn->FLT_MIN()) {
      if ($value->{y} == 0.25f) {
        if ($value->{z} == 0.5f) {
          return 1;
        }
      }
    }
    
    return 0;
  }
  
  static method return_value_double_method : TestCase::Point_3d () {
    my $value : TestCase::Point_3d;
    $value->{x} = Fn->DBL_MIN();
    $value->{y} = 0.25;
    $value->{z} = 0.5;
    
    return $value;
  }
  
  static method return_value_double : int () {
    my $value : TestCase::Point_3d = TestCase::Method->return_value_double_method();
    
    if ($value->{x} == Fn->DBL_MIN()) {
      if ($value->{y} == 0.25) {
        if ($value->{z} == 0.5) {
          return 1;
        }
      }
    }
    
    return 0;
  }
  
  static method method_push_arg_undef : TestCase::Minimal ($minimal : TestCase::Minimal) {
    return $minimal;
  }

  static method push_arg_undef : int () {
  
    my $minimal = TestCase::Method->method_push_arg_undef(undef);
    
    if ($minimal == undef) {
      return 1;
    }
    
    return 0;
  }

  # Default return value empty
  static method default_return_value_byte : int () {
    
    if (TestCase::Method->default_return_value_byte_method() == 0) {
      if (TestCase::Method->default_return_value_byte_method_empty() == 0) {
        return 1;
      }
    }
    
    return 0;
  }
  static method default_return_value_byte_method : byte () {
    1;
    return 0;
  }
  static method default_return_value_byte_method_empty : byte () {
    
    return 0;
  }
  static method default_return_value_short : int () {
    
    if ((int)TestCase::Method->default_return_value_short_method() == (int)(short)0) {
      if ((int)TestCase::Method->default_return_value_short_method_empty() == (int)(short)0) {
        return 1;
      }
    }
    
    return 0;
  }
  static method default_return_value_short_method : short () {
    1;
    return 0;
  }
  static method default_return_value_short_method_empty : short () {
    
    return 0;
  }
  static method default_return_value_int : int () {
    
    if (TestCase::Method->default_return_value_int_method() == (int)0) {
      if (TestCase::Method->default_return_value_int_method_empty() == (int)0) {
        return 1;
      }
    }
    
    return 0;
  }
  static method default_return_value_int_method : int () {
    1;
    return 0;
  }
  static method default_return_value_int_method_empty : int () {
    
    return 0;
  }
  static method default_return_value_long : int () {
    
    if (TestCase::Method->default_return_value_long_method() == (long)0) {
      if (TestCase::Method->default_return_value_long_method_empty() == (long)0) {
        return 1;
      }
    }
    
    return 0;
  }
  static method default_return_value_long_method : long () {
    1;
    return 0;
  }
  static method default_return_value_long_method_empty : long () {
    
    return 0;
  }
  static method default_return_value_float : int () {
    
    if (TestCase::Method->default_return_value_float_method() == (float)0) {
      if (TestCase::Method->default_return_value_float_method_empty() == (float)0) {
        return 1;
      }
    }
    
    return 0;
  }
  static method default_return_value_float_method : float () {
    1;
    return 0;
  }
  static method default_return_value_float_method_empty : float () {
    
    return 0;
  }
  static method default_return_value_double : int () {
    
    if (TestCase::Method->default_return_value_double_method() == (double)0) {
      if (TestCase::Method->default_return_value_double_method_empty() == (double)0) {
        return 1;
      }
    }
    
    return 0;
  }
  static method default_return_value_double_method : double () {
    1;
    return 0;
  }
  static method default_return_value_double_method_empty : double () {
    
    return 0;
  }
  static method default_return_value_object : int () {
    
    if (TestCase::Method->default_return_value_object_method() == undef) {
      if (TestCase::Method->default_return_value_object_method_empty() == undef) {
        return 1;
      }
    }
    
    return 0;
  }
  static method default_return_value_object_method : TestCase::Minimal () {
    1;
    return undef;
  }
  static method default_return_value_object_method_empty : TestCase::Minimal () {
    
    return undef;
  }
  
  # Call void function
  static method call_void_method : void ($nums : int[]) {
    $nums->[0] = 5;
  }
  static method call_void : int () {
    my $nums = [1];
    
    TestCase::Method->call_void_method($nums);
    
    if ($nums->[0] == 5) {
      return 1;
    }
    return 0;
  }
  
  static method call_spvm_method_nest : int () {
    my $total = TestCase::Method->call_spvm_method_nest_sum( TestCase::Method->call_spvm_method_nest_sum(1, 2), TestCase::Method->call_spvm_method_nest_sum(3, 4));
    
    if ($total == 10) {
      return 1;
    }
    
    return 0;
  }
  static method call_spvm_method_nest_sum : int ($num1 : int, $num2 : int) {
    return $num1 + $num2;
  }
  static method call_spvm_method_args_convertion_stab1 : double ($var1 : byte, $var2 : short, $var3 : int, $var4 : long, $var5 : float, $var6 : double) {
    return ($var1 + $var2 + $var3 + $var4 + $var5 + $var6);
  }
  
  static method call_spvm_method_args_convertion : int () {
    # Constant assignment
    my $success_constant_narrow = 0;
    {
      my $return_value1 = TestCase::Method->call_spvm_method_args_convertion_stab1(1, 2, 3, 2, 8, 16);
      if ($return_value1 == 32) {
        $success_constant_narrow = 1;
      }
    }
    
    # Widning
    my $success_constant_wide = 0;
    {
      my $return_value1 = TestCase::Method->call_spvm_method_args_convertion_stab1((byte)1, (byte)2, (byte)3, (byte)2, (byte)8, (byte)16);
      
      if ($return_value1 == 32) {
        $success_constant_wide = 1;
      }
    }
    
    if ($success_constant_narrow && $success_constant_wide) {
      return 1;
    }
    
    return 0;
  }

  # call_spvm_method arguments
  static method call_spvm_method_args_byte : int ($var1 : byte, $var2 : byte, $var3 : byte) {
    if ($var1 == 0) {
      if ($var2 == 127) {
        if ($var3 == -128) {
          return 1;
        }
      }
    }
    
    return 0;
  }
  static method call_spvm_method_args_short : int ($var1 : short, $var2 : short, $var3 : short) {
    if ((int)$var1 == (int)(short)0) {
      if ((int)$var2 == (int)(short)32767) {
        if ((int)$var3 == (int)(short)-32768) {
          return 1;
        }
      }
    }
    
    return 0;
  }
  static method call_spvm_method_args_int : int ($var1 : int, $var2 : int, $var3 : int) {
    if ($var1 == 0) {
      if ($var2 == 2147483647) {
        if ($var3 == -2147483648) {
          return 1;
        }
      }
    }
    
    return 0;
  }
  
  static method call_spvm_method_args_long : int ($var1 : long, $var2 : long, $var3 : long) {
    if ($var1 == 0L) {
      if ($var2 == 9223372036854775807L) {
        if ($var3 == -9223372036854775808L) {
          return 1;
        }
      }
    }
    
    return 0;
  }
  # byte array argument
  static method call_spvm_method_byte_array : byte ($nums : byte[]) {
    
    my $total = (byte)0;
    for (my $i = 0; $i < @$nums; $i++) {
      $total = (byte)((int)$total + (int)$nums->[$i]);
    }
    
    return $total;
  }
  
  # short array argument
  static method call_spvm_method_short_array : short ($nums : short[]) {
    
    my $total = (short)0;
    for (my $i = 0; $i < @$nums; $i++) {
      $total = (short)((int)$total + (int)$nums->[$i]);
    }
    
    return $total;
  }
  
  # int array argument
  static method call_spvm_method_int_array : int ($nums : int[]) {
    
    my $total = 0;
    for (my $i = 0; $i < @$nums; $i++) {
      $total = $total + $nums->[$i];
    }
    
    return $total;
  }

  # long array argument
  static method call_spvm_method_long_array : long ($nums : long[]) {
    
    my $total = (long)0;
    for (my $i = 0; $i < @$nums; $i++) {
      $total = $total + $nums->[$i];
    }
    
    return $total;
  }

  # float array argument
  static method call_spvm_method_float_array : float ($nums : float[]) {
    
    my $total = (float)0;
    for (my $i = 0; $i < @$nums; $i++) {
      $total = $total + $nums->[$i];
    }
    
    return $total;
  }

  # double array argument
  static method call_spvm_method_double_array : double ($nums : double[]) {
    
    my $total = (double)0;
    for (my $i = 0; $i < @$nums; $i++) {
      $total = $total + $nums->[$i];
    }
    
    return $total;
  }
  # call_spvm_method return value
  static method call_spvm_method_return_byte_array  : byte[] () {
    my $nums = new byte[3];
    
    $nums->[0] = (byte)1;
    $nums->[1] = (byte)2;
    $nums->[2] = (byte)3;
    
    return $nums;
  }
  static method call_spvm_method_return_byte_array_check : int ($nums : byte[]) {
    
    if ($nums->[0] == 1) {
      if ($nums->[1] == 2) {
        if ($nums->[2] == 3) {
          if (@$nums == 3) {
            return 1;
          }
        }
      }
    }
    
    return 0;
  }

  static method call_spvm_method_return_short_array : short[] () {
    my $nums = new short[3];
    
    $nums->[0] = (short)1;
    $nums->[1] = (short)2;
    $nums->[2] = (short)3;
    
    return $nums;
  }
  static method call_spvm_method_return_short_array_check : int ($nums : short[]) {
    
    if ((int)$nums->[0] == (int)(short)1) {
      if ((int)$nums->[1] == (int)(short)2) {
        if ((int)$nums->[2] == (int)(short)3) {
          if (@$nums == 3) {
            return 1;
          }
        }
      }
    }
    
    return 0;
  }

  static method call_spvm_method_return_int_array : int[] () {
    my $nums = new int[3];
    
    $nums->[0] = (int)1;
    $nums->[1] = (int)2;
    $nums->[2] = (int)3;
    
    return $nums;
  }
  static method call_spvm_method_return_int_array_check : int ($nums : int[]) {
    
    if ($nums->[0] == (int)1) {
      if ($nums->[1] == (int)2) {
        if ($nums->[2] == (int)3) {
          if (@$nums == 3) {
            return 1;
          }
        }
      }
    }
    
    return 0;
  }

  static method call_spvm_method_return_long_array : long[] () {
    my $nums = new long[3];
    
    $nums->[0] = (long)1;
    $nums->[1] = (long)2;
    $nums->[2] = (long)3;
    
    return $nums;
  }
  static method call_spvm_method_return_long_array_check : int ($nums : long[]) {
    
    if ($nums->[0] == (long)1) {
      if ($nums->[1] == (long)2) {
        if ($nums->[2] == (long)3) {
          if (@$nums == 3) {
            return 1;
          }
        }
      }
    }
    
    return 0;
  }

  static method call_spvm_method_return_float_array : float[] () {
    my $nums = new float[3];
    
    $nums->[0] = (float)1;
    $nums->[1] = (float)2;
    $nums->[2] = (float)3;
    
    return $nums;
  }
  static method call_spvm_method_return_float_array_check : int ($nums : float[]) {
    
    if ($nums->[0] == (float)1) {
      if ($nums->[1] == (float)2) {
        if ($nums->[2] == (float)3) {
          if (@$nums == 3) {
            return 1;
          }
        }
      }
    }
    
    return 0;
  }

  static method call_spvm_method_return_double_array : double[] () {
    my $nums = new double[3];
    
    $nums->[0] = (double)1;
    $nums->[1] = (double)2;
    $nums->[2] = (double)3;
    
    return $nums;
  }
  static method call_spvm_method_return_double_array_check : int ($nums : double[]) {
    
    if ($nums->[0] == (double)1) {
      if ($nums->[1] == (double)2) {
        if ($nums->[2] == (double)3) {
          if (@$nums == 3) {
            return 1;
          }
        }
      }
    }
    
    return 0;
  }
  static method call_spvm_method_assign : int () {
    my $m = TestCase::Minimal->new();
    $m = TestCase::Minimal->new();
    return 0;
  }
  
  static method call_spvm_method_undef : int ($value : TestCase::Minimal) {
    
    if ($value == undef) {
      return 1;
    }
    
    return 0;
  }

  static method call_spvm_method_last_camma : int () {
    
    my $total =  TestCase::Method->sum_int(3, 4,);
    
    if ($total == 7) {
      return 1;
    }
    
    return 0;
  }
  static method sum_int : int ($a : int, $b :int) {
    
    my $total = $a + $b;
    
    return $total;
  }

}
