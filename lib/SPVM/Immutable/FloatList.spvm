# Copyright (c) 2023 Yuki Kimoto
# MIT License

class Immutable::FloatList {
  version_from SPVM;

  use Fn;
  use Array;
  
  # Interfaces
  interface Countable;
  
  # Fields
  has length : ro int;
  has array : float[];
  
  # Class methods
  static method new : Immutable::FloatList ($array : float[] = undef) {
    my $length : int;
    if ($array) {
      $length = @$array;
    }
    else {
      $length = 0;
    }
    
    my $self = &new_len($length);
    
    if ($array) {
      Array->memcpy_float($self->{array}, 0, $array, 0, $length);
    }
    
    return $self;
  }
  
  static method new_len : Immutable::FloatList ($length : int) {
    my $self = new Immutable::FloatList;
    
    unless ($length >= 0) {
      die "The length \$length must be greater than or equal to 0.";
    }
    
    $self->{length} = $length;
    $self->{array} = new float[$length];
    
    return $self;
  }
  
  # Instance methods
  method get : float ($index : int) {
    my $length = $self->length;
    
    unless ($index >= 0) {
      $index = $length + $index;
    }
    
    unless ($index >= 0) {
      die "The index \$index must be greater than or equal to 0.";
    }
    
    unless ($index < $length) {
      die "The index \$index must be less than the length of \$list.";
    }
    
    my $element = $self->{array}[$index];
    
    return $element;
  }
  
  method to_array : float[] () {
    my $length = $self->length;
    
    my $new_array = new float[$length];
    
    my $array = $self->{array};
    
    Array->memcpy_float($new_array, 0, $array, 0, $length);
    
    return $new_array;
  }
}
