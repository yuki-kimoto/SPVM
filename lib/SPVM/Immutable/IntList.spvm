# Copyright (c) 2023 Yuki Kimoto
# MIT License

class Immutable::IntList {
  version_from SPVM;

  use Fn;
  use Array;
  
  # Interfaces
  interface Countable;
  
  # Fields
  has length : ro int;
  has array : int[];
  
  # Class methods
  static method new : Immutable::IntList ($array : int[] = undef) {
    my $length : int;
    if ($array) {
      $length = @$array;
    }
    else {
      $length = 0;
    }
    
    my $self = &new_len($length);
    
    if ($array) {
      Array->memcpy_int($self->{array}, 0, $array, 0, $length);
    }
    
    return $self;
  }
  
  static method new_len : Immutable::IntList ($length : int) {
    my $self = new Immutable::IntList;
    
    unless ($length >= 0) {
      die "The length \$length must be greater than or equal to 0.";
    }
    
    $self->{length} = $length;
    $self->{array} = new int[$length];
    
    return $self;
  }
  
  # Instance methods
  method get : int ($index : int) {
    my $length = $self->length;
    
    unless ($index >= 0) {
      $index = $length + $index;
    }
    
    unless ($index >= 0) {
      die "The index \$index must be greater than or equal to 0.";
    }
    
    unless ($index < $length) {
      die "The index \$index must be less than the length of \$list.";
    }
    
    my $element = $self->{array}[$index];
    
    return $element;
  }
  
  method to_array : int[] () {
    my $length = $self->length;
    
    my $new_array = new int[$length];
    
    my $array = $self->{array};
    
    Array->memcpy_int($new_array, 0, $array, 0, $length);
    
    return $new_array;
  }
  
}
