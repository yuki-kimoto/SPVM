class DoubleList {
  use Fn;
  use Array;
  
  has values   : double[];
  has capacity : int;
  has length   : ro int;
  
  private enum {
    DEFAULT_CAPACITY = 16,
  }

  static method new : DoubleList ($array : double[]...) {
    my $self = new DoubleList;
    my $length : int;
    if ($array) {
      $length = @$array;
    }
    else {
      $length = 0;
    }
    
    my $capacity = 0;
    if ($length < DoubleList->DEFAULT_CAPACITY()) {
      $capacity = DoubleList->DEFAULT_CAPACITY();
    }
    else {
      $capacity = $length;
    }
    $self->{capacity} = $capacity;
    my $values = new double[$capacity];
    
    $self->{length} = $length;
    
    if ($array) {
      Array->memcpy_double($values, 0, $array, 0, $length);
    }
    
    $self->{values} = $values;
    
    return $self;
  }

  static method new_len : DoubleList ($length : int) {
    my $self = new DoubleList;
    unless ($length >= 0) {
      die "length must be more than or equals to zero";
    }
    my $capacity = 0;
    if ($length < DoubleList->DEFAULT_CAPACITY()) {
      $capacity = DoubleList->DEFAULT_CAPACITY();
    }
    else {
      $capacity = $length;
    }
    $self->{capacity} = $capacity;
    $self->{length} = $length;
    $self->{values} = new double[$capacity];
    return $self;
  }
  
  precompile private method _extend_with_capacity : void ($new_capacity : int) {
    my $new_values = new double [$new_capacity];
    for (my $i = 0; $i < $self->{length}; ++$i) {
      my $target = $i;
      if ($target < 0) { $target += $self->{capacity}; };
      $new_values->[$i] = $self->{values}[$target];
    }
    $self->{capacity} = $new_capacity;
    $self->{values} = $new_values;
  }

  private method _extend : void () {
    my $capacity = $self->{capacity};
    my $length = $self->{length};
    my $values = $self->{values};
    
    my $new_capacity = $capacity * 2;
    
    my $new_values = new double[$new_capacity];
    
    Array->memcpy_double($new_values, 0, $values, 0, $length);
    
    $self->{values} = $new_values;
    $self->{capacity} = $new_capacity;
  }
  
  method insert : void ($index : int, $value : double) {
    my $length = $self->{length};
    my $capacity = $self->{capacity};
    
    unless ($index >= 0 && $index <= $length) {
      die "Out of range";
    }
    
    if ($length >= $capacity) {
      $self->_extend;
    }
    
    my $values = $self->{values};
    if ($index != $length) {
      my $dist_index = $index + 1;
      my $move_length = $length - $index;
      Array->memmove_double($values, $dist_index, $values, $index, $move_length);
    }
    $values->[$index] = $value;
    
    $self->{length}++;
  }

  method remove : double ($index : int) {
    my $length = $self->{length};
    my $capacity = $self->{capacity};
    
    unless ($index >= 0 && $index < $length) {
      die "Out of range";
    }
    
    my $values = $self->{values};
    my $remove_value = $values->[$index];

    my $dist_index = $index;
    my $src_index = $index + 1;
    my $move_length = $length - $index - 1;
    Array->memmove_double($values, $dist_index, $values, $src_index, $move_length);
    
    $self->{length}--;
    
    return $remove_value;
  }

  method push : void ($value : double) {
    my $length = $self->{length};
    my $capacity = $self->{capacity};
    
    if ($length >= $capacity) {
      $self->_extend;
    }
    
    $self->{values}[$length] = $value;
    
    $self->{length}++;
  }
  
  method pop : double () {
    my $length = $self->length;
    
    if ($length == 0) {
      die "Can't pop";
    }
    
    my $value = $self->{values}[$length - 1];

    $self->{values}[$length - 1] = 0;
    
    $self->{length}--;
    
    return $value;
  }
  
  method unshift : void ($value : double) {
    my $length = $self->{length};
    my $capacity = $self->{capacity};
    
    if ($length >= $capacity) {
      $self->_extend;
    }
    
    my $values = $self->{values};
    
    Array->memmove_double($values, 1, $values, 0, $length);
    
    $values->[0] = $value;
    $self->{length}++;
  }
  
  method shift : double () {
    my $length = $self->{length};
    my $capacity = $self->{capacity};
    
    if ($length == 0) {
      die "Can't shift";
    }
    
    my $values = $self->{values};
    
    my $value = $values->[0];
    
    Array->memmove_double($values, 0, $values, 1, $length - 1);
    
    $values->[$length - 1] = 0;
    
    $self->{length}--;
    
    return $value;
  }
  
  method set : void ($index : int, $value : double) {
    my $length = $self->length;
    
    if ($index < 0 || $index > $length - 1) {
      die "Out of range";
    }
    
    $self->{values}[$index] = $value;
  }
  
  method set_array : void ($array : double[]) {
    unless ($array) {
      die "Array must be defined";
    }
    
    my $cur_length = $self->length;
    
    my $set_length = @$array;
    
    unless ($set_length == $cur_length) {
      die "The length of argument array must be same as the length of current list array";
    }
    
    Array->memcpy_double($self->{values}, 0, $array, 0, $cur_length);
  }

  method get : double ($index : int) {
    my $length = $self->length;
    
    if ($index < 0 || $index > $length - 1) {
      die "Out of range";
    }
    
    my $value = $self->{values}[$index];
    
    return $value;
  }
  
  method to_array : double[] () {
    my $length = $self->length;
    
    my $array = new double[$length];
    
    my $values = $self->{values};
    
    Array->memcpy_double($array, 0, $values, 0, $length);
    
    return $array;
  }

  method resize : void ($new_length : int) {
    if ($new_length < 0) {
      die "New length must be more than or equals to 0";
    }
    
    my $length = $self->{length};
    my $capacity = $self->{capacity};
    
    if ($new_length > $length) {
      if ($new_length > $capacity) {
        $self->_extend_with_capacity($new_length);
      }
      Array->memset_double($self->{values}, $length, 0, $new_length - $length);
    }
    elsif ($new_length < $length) {
      Array->memset_double($self->{values}, $new_length, 0, $length - $new_length);
    }
    
    $self->{length} = $new_length;
  }
}
